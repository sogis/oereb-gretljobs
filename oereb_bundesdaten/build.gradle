import ch.so.agi.gretl.api.TransferSet
import ch.so.agi.gretl.tasks.*
import java.nio.file.Paths
import de.undercouch.gradle.tasks.download.Download

apply plugin: "de.undercouch.download"
apply plugin: "ch.so.agi.gretl"

defaultTasks "oereb_dbImport_Daten"


// Download-Verzeichnis
def pathToTempFolder = System.getProperty("java.io.tmpdir")
def pathToUnzipFolder = Paths.get(pathToTempFolder, "unzip_data")
def pathToDataZip = Paths.get(pathToTempFolder, "data.zip")

// Oereb-Themen Bundesdaten
def oerebThemen = [
                   "ch.astra.baulinien-nationalstrassen.oereb",
                   "ch.astra.projektierungszonen-nationalstrassen.oereb",
                   "ch.bav.baulinien-eisenbahnanlagen.oereb",
                   "ch.bav.projektierungszonen-eisenbahnanlagen.oereb",
                   "ch.bazl.baulinien-flughafenanlagen.oereb",
                   "ch.bazl.projektierungszonen-flughafenanlagen.oereb",
                   "ch.bav.kataster-belasteter-standorte-oev.oereb",
                   "ch.bazl.kataster-belasteter-standorte-zivilflugplaetze.oereb",
                   "ch.bazl.sicherheitszonenplan.oereb"
                   ]

// Oereb-DB Schemas
def dbSchemas = [
                 "stage",
                 "live"
                 ]


// Oereb-Themen herunterladen und importieren
oerebThemen.each { oerebThema ->
    def themaName = oerebThema.toString()

    task "oereb_download_$themaName"(type: Download){
        description = "Download Bundesdaten"
        src "https://data.geo.admin.ch/" + themaName + "/data.zip"
        dest pathToTempFolder
        overwrite true
    }

    task "oereb_unzipData_$themaName"(type: Copy, dependsOn: "oereb_download_$themaName"){
        description = "Unzip Data.zip"
        from zipTree(pathToDataZip)
        into file(pathToUnzipFolder)
        include "**/*.xtf"
        rename { String fileName ->
            if (fileName.contains(themaName)) {
                return themaName + ".xtf"
            }
            return fileName
        }
    }

    dbSchemas.eachWithIndex { dbSchema, i ->
        def schema = dbSchema.toString()
        def index = i.toString()
        def taskIndex = index
    
        task "oereb_Import_$taskIndex$themaName$schema"(type: Ili2pgReplace, dependsOn: "oereb_unzipData_$themaName"){
            description = "Import Bundesdaten in DB schema stage"
            database = [dbUriOereb, dbUserOereb, dbPwdOereb]
            dbschema = schema
            dataset = themaName
            models = "OeREBKRMtrsfr_V1_1"
            dataFile = file(Paths.get(pathToUnzipFolder.toString(), themaName + ".xtf"))
            strokeArcs = true
        }
    }
    
}

task oereb_dbImport_Daten() {
    description = "Import der Oereb-Bundesdaten"
}

oereb_dbImport_Daten.dependsOn {
    tasks.findAll { task -> task.name.startsWith('oereb_Import_') }
}
