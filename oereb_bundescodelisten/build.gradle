import ch.so.agi.gretl.tasks.*
import java.nio.file.Paths
import de.undercouch.gradle.tasks.download.Download

defaultTasks "dbImport"

def pathToTempFolder = System.getProperty("java.io.tmpdir")
def pathToUnzipFolder = Paths.get(pathToTempFolder, "unzip_data")
def fileName = "OeREBKRM_V1_1_Codelisten_20170101.xml"
def iliModelOereb = "OeREBKRM_V1_1"

def GROUP = "Bundescodelisten-Import"

def dbSchemas = ["stage", "live"]

task download(type: Download) {
    group = GROUP
    description = "Download Bundescodelisten."
    src "http://models.geo.admin.ch/V_D/OeREB/" + fileName
    dest pathToTempFolder
    overwrite true

    doLast {
        println "File downloaded to: " + pathToTempFolder
    }
}

dbSchemas.each { dbSchema ->
    task "import_$dbSchema"(type: Ili2pgReplace, dependsOn: download) {
        group = GROUP
        description = "Import Bundescodelisten in die Datenbank."
        database = [dbUriOereb, dbUserOereb, dbPwdOereb]
        dbschema = dbSchema
        models = iliModelOereb
        dataFile = file(Paths.get(pathToTempFolder.toString(), fileName))
        dataset = fileName
        disableValidation = true

        doLast {
            println "Data imported into schema $dbSchema of db $dbUriOereb"
        }
    }
}

task dbImport() {
    description = "Aggregation task."
    doLast {
        println "Data import into db finished."
    }
}

dbImport.dependsOn {
    tasks.findAll { task -> task.name.startsWith('import_') }
}
